buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.13.RELEASE"
    }
}

// Check for latest dependencies: ./gradlew dependencyUpdates -Drevision=release
plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

description 'Spring gae CloudStorage Integration'
group 'com.threewks.spring'


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'wrapper'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

wrapper {
    gradleVersion = '5.6.4'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}


dependencies {
    compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile 'com.google.guava:guava:28.1-jre'
    compile 'com.google.http-client:google-http-client-appengine:1.34.2'
    compile('com.google.api-client:google-api-client-appengine:1.30.9') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    compile "com.google.cloud:google-cloud-storage:1.105.0"

    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'org.apache.commons:commons-io:1.3.2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def pomConfig = {
    scm {
        connection 'https://github.com/3wks/spring-gae-gcs.git'
        developerConnection 'https://github.com/3wks/spring-gae-gcs.git'
        url 'https://github.com/3wks/spring-gae-gcs.git'
    }

    licenses {
        license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'afcastano'
            name 'Andres Castano'
            email 'andres@3wks.com.au'
        }
        developer {
            id 'mbyrne00'
            name 'Matt Byrne'
            email 'matt@3wks.com.au'
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testSourcesJar(type: Jar) {
    classifier = 'testSources'
    from sourceSets.test.allSource
}

artifacts {
    archives javadocJar, sourcesJar, testSourcesJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', 'https://github.com/3wks/spring-gae-gcs')
                root.appendNode('packaging', 'jar')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true

    pkg {
        repo = project.name
        name = project.name
        desc = project.description
        repo = 'spring-google-appengine'
        userOrg = '3wks'
        licenses = ['MIT']
        labels = ['spring', 'spring-boot', 'google', 'appengine', 'gae', 'gcp', 'cloudstorage']
        vcsUrl = 'https://github.com/3wks/spring-gae-gcs.git'
        version {
            name = project.version
            desc = "${project.name} ${project.version}"
            released = new Date()
            gpg {
                sign = true
                passphrase = System.getProperty('signing.passphrase')
            }
            mavenCentralSync {
                sync = true
                user = System.getProperty('oss.user')
                password = System.getProperty('oss.password')
                close = '1'
            }
        }
    }
}
